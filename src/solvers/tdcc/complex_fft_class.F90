!
!
!  eT - a coupled cluster program
!  Copyright (C) 2016-2021 the authors of eT
!
!  eT is free software: you can redistribute it and/or modify
!  it under the terms of the GNU General Public License as published by
!  the Free Software Foundation, either version 3 of the License, or
!  (at your option) any later version.
!
!  eT is distributed in the hope that it will be useful,
!  but WITHOUT ANY WARRANTY; without even the implied warranty of
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
!  GNU General Public License for more details.
!
!  You should have received a copy of the GNU General Public License
!  along with this program. If not, see <https://www.gnu.org/licenses/>.
!
!
module complex_fft_class
!
!!
!! Complex FFT solver class module
!! Written by Andreas Skeidsvoll, Feb 2019
!!
!! Module containing the type complex_fft, of which objects can be generated by calling
!! new_complex_fft(tag, file_name). These objects are used for reading and complex fast Fourier
!! transforming (FFT) a complex time series from the file file_name, which should contain lines on
!! the format
!!
!!    real_time real_x_val imag_x_val real_y_val imag_y_val real_z_val imag_z_val
!!
!! The time series file can be generated by using cc propagation solver. The angular frequency
!! series generated by this solver is saved linewise in the file
!! complex_fft_file_name_to_angular_frequency on the format
!!
!!    real_angular_frequency real_x_val imag_x_val real_y_val imag_y_val real_z_val imag_z_val
!!
!! The complex fast Fourier transformation itself is done using a modified version of modern
!! FFTPACK by Jon Lo Kim Lin
!!
!
   use fftpack_complex_forward_1d
   use fftpack_complex_initialization_routines
!
   use memory_manager_class, only: mem
   use global_in, only : input
   use global_out, only : output
   use sequential_file_class, only : sequential_file
!
   implicit none
!
   type :: complex_fft
!
      character(len=100) :: name_, tag, file_name
!
      character(len=500) :: description
      character(len=500) :: summary
!
      real(dp) :: ti, tf, h
!
      type(sequential_file) :: time_series_file, angular_frequency_series_file
!
   contains
!
      procedure :: run           => run_complex_fft
      procedure :: cleanup       => cleanup_complex_fft
!
      procedure :: print_banner  => print_banner_complex_fft
      procedure :: print_summary => print_summary_complex_fft
!
      procedure :: read_settings => read_settings_complex_fft
!
   end type complex_fft
!
!
   interface complex_fft
!
      procedure :: new_complex_fft
!
   end interface complex_fft
!
contains
!
!
   function new_complex_fft(tag, file_name) result(solver)
!!
!!    New complex FFT
!!    Written by Andreas Skeidsvoll, Feb 2019
!!
!!    Generate a new complex FFT solver object.
!!
      implicit none
!
      type(complex_fft) :: solver
!
      character(*), intent(in) :: tag, file_name
!
      solver%file_name = trim(file_name)
      solver%tag = trim(tag)
!
      solver%name_ = 'Complex FFT ' // trim(solver%tag) // ' solver'
      solver%description = 'A solver that calculates the complex fast Fourier transform (FFT) '  &
                           // 'of the ' // trim(solver%tag) // ' time series by using modified ' &
                           // 'FFTPACK routines.'
      solver%summary = 'Complex FFT finished successfully, angular frequency series has been ' &
                       // 'written to file.'
!
      call solver%print_banner()
!
      call solver%read_settings()
!
      solver%time_series_file = sequential_file(trim(solver%file_name), 'formatted')
      solver%angular_frequency_series_file = sequential_file('complex_fft_'              &
                                                             // trim(solver%file_name)   &
                                                             // '_to_angular_frequency', &
                                                             'formatted')
!
   end function new_complex_fft
!
!
   subroutine run_complex_fft(solver)
!!
!!    Run complex FFT
!!    Written by Andreas Skeidsvoll, Feb 2019
!!
!!    Do the complex fast Fourier transform (complex FFT) of the time series to get an angular
!!    frequency spectrum.
!!
      use iso_fortran_env, only: iostat_end
!
      implicit none 
!
      class(complex_fft) :: solver
!
      integer :: io, i, work_save_length, work_length, error_index, n_unused_lines, &
                 n_complex_fft_lines
!
      real(dp) :: t, sampling_angular_frequency, angular_frequency_resolution
!
      complex(dp), dimension(:), allocatable :: fft_x_vector, fft_y_vector, fft_z_vector
!
      real(dp), dimension(:), allocatable :: angular_frequency_values, work_save_array, &
                                             work_array, row_to_read, row_to_write
!
      call solver%time_series_file%open_('read', 'rewind')
!
!     Get number of unused lines before the start of the time series, stop reading if
!     reaching end of file
!
      n_unused_lines = 0
      n_complex_fft_lines = 0
!
      do
!
         call solver%time_series_file%read_(t, io_stat=io)
!
!        Reached initial time of time series, or end of file
!
         if ((t .ge. solver%ti) .or. (io .eq. iostat_end)) then
            n_complex_fft_lines = 1
            exit
!
         elseif (io .ne. 0) then
            call output%error_msg('complex FFT file reading failed.')
!
         endif
!
         n_unused_lines = n_unused_lines + 1
!
      enddo
!
!     Get number of lines after the start and before the end of the time series, stop reading if
!     reaching end of file
!
      do
!
         call solver%time_series_file%read_(t, io_stat=io)
!
!        Reached initial time of time series, or end of file
!
         if ((t .gt. solver%tf) .or. (io .eq. iostat_end)) then
            exit
!
         elseif (io .ne. 0) then
            call output%error_msg('complex FFT file reading failed.')
!
         endif
!
         n_complex_fft_lines = n_complex_fft_lines + 1
!
      enddo
!
!
      call solver%time_series_file%rewind_
!
!     Read time series from file
!
      call mem%alloc(fft_x_vector, n_complex_fft_lines)
      call mem%alloc(fft_y_vector, n_complex_fft_lines)
      call mem%alloc(fft_z_vector, n_complex_fft_lines)
!
      do i = 1, n_unused_lines
         call solver%time_series_file%read_(t)
      enddo
!
      call mem%alloc(row_to_read, 7)
!
      do i = 1, n_complex_fft_lines
!
         call solver%time_series_file%read_(row_to_read, 7)
!
         fft_x_vector(i) = cmplx(row_to_read(2), row_to_read(3), dp)
         fft_y_vector(i) = cmplx(row_to_read(4), row_to_read(5), dp)
         fft_z_vector(i) = cmplx(row_to_read(6), row_to_read(7), dp)
!
      enddo
!
      call mem%dealloc(row_to_read, 7)
!
      call solver%time_series_file%close_
!
!     Make vector of angular frequency values
!
      sampling_angular_frequency = (eight*atan(one))*(one/solver%h) ! 2*Ï€/dt
      angular_frequency_resolution = sampling_angular_frequency/n_complex_fft_lines
!
      call mem%alloc(angular_frequency_values, n_complex_fft_lines)
!
      do i = 1, n_complex_fft_lines
         angular_frequency_values(i) = angular_frequency_resolution*(i-1)
      enddo
!
!     Initialize work_save_array, which is needed for complex FFT with FFTPACK
!
      work_save_length = 2*(n_complex_fft_lines) + &
                         int(log(real(n_complex_fft_lines, dp))/log(two)) + 4
      call mem%alloc(work_save_array, work_save_length)
!
      call cfft1i(n_complex_fft_lines, work_save_array(1:work_save_length), work_save_length, &
                  error_index)
!
      if (error_index .ne. 0) then
         call output%error_msg('could not initialize work_save_array, which is used used for FFT')
      endif
!
!     Do complex FFT of time series using FFTPACK
!
      work_length = 2*n_complex_fft_lines
      call mem%alloc(work_array, work_length)
!
      call cfft1f(n_complex_fft_lines, 1, fft_x_vector, n_complex_fft_lines, work_save_array, &
                  work_save_length, work_array, work_length, error_index)
!
      if (error_index .ne. 0) then
            call output%error_msg('complex FFT of the time series in the x-direction failed')
      endif
!
      call cfft1f(n_complex_fft_lines, 1, fft_y_vector, n_complex_fft_lines, work_save_array, &
                  work_save_length, work_array, work_length, error_index)
!
      if (error_index .ne. 0) then
         call output%error_msg('complex FFT of the time series in the y-direction failed')
      endif
!
      call cfft1f(n_complex_fft_lines, 1, fft_z_vector, n_complex_fft_lines, work_save_array, &
                  work_save_length, work_array, work_length, error_index)
!
      if (error_index .ne. 0) then
         call output%error_msg('complex FFT of the time series in the z-direction failed')
      endif
!
      call mem%dealloc(work_save_array, work_save_length)
      call mem%dealloc(work_array, work_length)
!
!     Write angular frequency series to file
!
      call solver%angular_frequency_series_file%open_('write', 'rewind')
!
      call mem%alloc(row_to_write, 7)
!
      do i = 1, n_complex_fft_lines
!
!        The half of the angular frequency series corresponding to the largest angular frequencies
!        are removed (due to Nyquist's theorem)
!
         if (angular_frequency_values(i) .lt. sampling_angular_frequency/two) then
!
            row_to_write = (/ angular_frequency_values(i),                  &
                             real(fft_x_vector(i)), aimag(fft_x_vector(i)), &
                             real(fft_y_vector(i)), aimag(fft_y_vector(i)), &
                             real(fft_z_vector(i)), aimag(fft_z_vector(i))  /)
!
            call solver%angular_frequency_series_file%write_(row_to_write, 7)
!
         endif
!
      enddo
!
      call mem%dealloc(row_to_write, 7)
      call mem%dealloc(fft_x_vector, n_complex_fft_lines)
      call mem%dealloc(fft_y_vector, n_complex_fft_lines)
      call mem%dealloc(fft_z_vector, n_complex_fft_lines)
      call mem%dealloc(angular_frequency_values, n_complex_fft_lines)
!
      call solver%angular_frequency_series_file%close_
!
   end subroutine run_complex_fft
!
!
   subroutine cleanup_complex_fft(solver)
!!
!!    Cleanup
!!    Written by Andreas Skeidsvoll, Feb 2019
!!
!!    Clean up the complex FFT solver object.
!!
      implicit none
!
      class(complex_fft), intent(inout) :: solver
!
      call solver%print_summary()
!
   end subroutine cleanup_complex_fft
!
!
   subroutine print_banner_complex_fft(solver)
!!
!!    Print banner
!!    Written by Andreas Skeidsvoll, Feb 2019
!!
!!    Print the banner of the solver.
!!
      implicit none 
!
      class(complex_fft) :: solver
!
      call output%printf('m', ' - ' // trim(solver%name_), fs='(/t3,a)')
      call output%print_separator('m', len(trim(solver%name_)) + 6, '-')
!
      call output%printf('n', solver%description, ffs='(/t3,a)', fs='(t3,a)')
!
   end subroutine print_banner_complex_fft
!
!
   subroutine print_summary_complex_fft(solver)
!!
!!    Print summary 
!!    Written by Andreas Skeidsvoll, Feb 2019
!!
!!    Print the summary of the solver.
!!
      implicit none 
!
      class(complex_fft) :: solver
!
      call output%printf('n', solver%summary, ffs='(/t3,a)', fs='(t3,a)')
!
   end subroutine print_summary_complex_fft
!
!
   subroutine read_settings_complex_fft(solver)
!!
!!    Read settings
!!    Written by Andreas Skeidsvoll, May 2019 
!!
!!    Read the settings required to do the complex FFT.
!!
!!    Settings:
!!
!!    initial time: the solver starts considering the values of the time series when reaching this
!!                  time
!!    final time:   the solver stops considering the values of the time series when reaching this
!!                  time
!!    time step:    the temporal spacing between the values of the time series
!!
      implicit none
!
      class(complex_fft) :: solver
!
      call input%get_required_keyword( &
         'initial time', 'solver fft ' // trim(solver%tag), solver%ti)
      call input%get_required_keyword( &
         'final time', 'solver fft ' // trim(solver%tag), solver%tf)
      call input%get_required_keyword( &
         'time step', 'solver fft ' // trim(solver%tag), solver%h)
!
   end subroutine read_settings_complex_fft
!
!
end module complex_fft_class